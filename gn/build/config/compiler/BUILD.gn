import("//gn/build/config/compiler/compiler.gni")

# default include directories.
config("default_include_dirs") {
  include_dirs = [
    "//",
    root_gen_dir,
  ]
}

config("warning") {
  configs = []

  if (is_win) {
    configs += [ "//gn/build/config/win:warning" ]
  }
}

# compilers
config("compiler") {
  asmflags = []
  cflags = []
  cflags_c = []
  cflags_cc = []
  defines = []
  ldflags = []
  libs = []
  configs = []

  if (is_win) {
    configs += [ "//gn/build/config/win:compiler" ]
  } else if (is_android) {
    configs += [ "//gn/build/config/android:compiler" ]
  } else if (is_linux) {
    configs += [ "//gn/build/config/linux:compiler" ]
  } else if (is_mac) {
    configs += [ "//gn/build/config/mac:compiler" ]
  }

  configs += [ ":compiler_cpu_abi" ]

  cflags_c += [ "-std=gnu11" ]
  cflags_cc += [ "-std=c++0x" ]

  if (is_linux || is_android) {
    cflags += [
      "-fPIC",
      "-pipe",
    ]
    ldflags += [ "-fPIC" ]
  }
}

config("compiler_cpu_abi") {
  cflags = []
  ldflags = []

  if (is_posix && !is_mac) {
    if (current_cpu == "x64") {
      cflags += [
        "-m64",
        "-march=x86-64",
      ]
      ldflags += [ "-m64" ]
    } else if (current_cpu == "x86") {
      cflags += [
        "-m32",
        "-m32",
      ]
    }
  }

  asmflags = cflags
}
