import("//build/config/android/config.gni")
import("//build/config/android/internal_rules.gni")
import("//build/config/android/rules.gni")

# inputs:
#   build_config
#
template("android_aar_internal") {
  assert(defined(invoker.build_config))

  _template_name = target_name
  _build_config = read_file(invoker.build_config, "json")
  _final_deps = []

  _deps = []
  if (defined(invoker.deps)) {
    _deps += invoker.deps
  }

  foreach(_target_config, _build_config.target_list) {
    print(_target_config.name)

    target(_target_config.type, _target_config.name) {
      deps = _deps

      if (_target_config.type == "android_resources") {
        forward_variables_from(_target_config,
                               [
                                 "resource_dirs",
                                 "android_manifest",
                               ])
      } else if (_target_config.type == "java_prebuilt") {
        forward_variables_from(_target_config,
                               [
                                 "jar_path",
                                 "requires_android",
                                 "supports_android",
                               ])
      } else {
        assert(false, "type ${_target_config.type} is not supported.")
      }
    }

    _final_deps += [ ":${_target_config.name}" ]
  }

  java_group(_template_name) {
    deps = _final_deps
  }
}

# android_aar_internal("appcompat_resources") {
#   build_config = "appcompat_resources__build_config.json"
# }

# Build an andorid aar file.
#
# inputs:
#   aar_path
#   maven_depname
#
template("android_aar") {
  assert(defined(invoker.aar_path))
  assert(defined(invoker.maven_depname))

  _template_name = target_name
  _base_path = "$target_gen_dir/${target_name}__android_aar"

  _json_path = _base_path + ".json"

  exec_script("//build/android/gn/generate_aar_targets.py",
              [
                "--name",
                _template_name,
                "--maven-depname",
                invoker.maven_depname,
                "--root-dir",
                rebase_path("//", root_build_dir),
                "--m2-home",
                rebase_path("//src/m2", root_build_dir),
                "--aar-path",
                rebase_path(invoker.aar_path, root_build_dir),
                "--json-path",
                rebase_path(_json_path, root_build_dir),
              ])

  android_aar_internal(_template_name) {
    forward_variables_from(invoker, [ "deps" ])
    build_config = _json_path
  }
}

template("android_maven_aar") {
  assert(defined(invoker.aar_path))

  android_aar(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "aar_path",
                             "maven_depname",
                           ])
  }
}

template("android_maven_jar") {
  java_prebuilt(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "jar_path",
                           ])
    supports_android = true
  }
}

template("android_maven") {
  assert(defined(invoker.deps_list))

  _template_name = target_name
  _final_deps = []

  _maven_json_path =
      "$target_gen_dir/${target_name}__android_maven_metadata.json"
  _target_json_path =
      "$target_gen_dir/${target_name}__android_maven_targets.json"

  _artifact_list = []
  foreach(_artifact, invoker.deps_list) {
    _artifact_list += [
      "--artifact",
      _artifact,
    ]
  }

  exec_script("//build/android/gn/maven_download.py",
              [
                    "--m2-dir",
                    "../src/m2",
                    "--src-root",
                    "../",
                    "--output-json",
                    rebase_path(_maven_json_path, root_build_dir),
                    "--target-json",
                    rebase_path(_target_json_path, root_build_dir),
                  ] + _artifact_list,
              "")

  # print(_target_json_path)
  _target_json = read_file(_target_json_path, "json")

  # print(_target_json)

  foreach(_target_config, _target_json.deps_info) {
    _final_deps += [ ":${_target_config.name}" ]

    # print(_target_config)
    _target_type = _target_config.type

    _is_aar = _target_type == "android_maven_aar"
    _is_jar = _target_type == "android_maven_jar"

    if (_is_aar || _is_jar) {
      _file_path = _target_config.file_path
    }

    target(_target_config.type, _target_config.name) {
      forward_variables_from(_target_config, [ "deps" ])

      if (_is_aar) {
        aar_path = _file_path
        maven_depname = _target_config.maven_depname
      }
      if (_is_jar) {
        jar_path = _file_path
      }
    }
  }

  java_group(_template_name) {
    deps = _final_deps
  }
}

android_maven("appcompat") {
  deps_list = [ "androidx.appcompat:appcompat:1.1.0" ]
}

group("main") {
  deps = [
    ":appcompat",
  ]
}
