if (host_os == "win") {
  _host_executable_suffix = ".exe"
} else {
  _host_executable_suffix = ""
}

template("compiled_action") {
  assert(defined(invoker.tool))
  assert(defined(invoker.outputs))
  assert(defined(invoker.args))

  assert(!defined(invoker.sources))

  action(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "inputs",
                             "outputs",
                             "testonly",
                             "visibility",
                           ])
    if (!defined(deps)) {
      deps = []
    }
    if (!defined(inputs)) {
      inputs = []
    }

    script = "//build/gn_run_binary.py"

    host_tool = invoker.tool + "($host_toolchain)"
    host_executable =
        get_label_info("$host_tool", "root_out_dir") + "/" +
        get_label_info("$host_tool", "name") + _host_executable_suffix

    inputs += [ host_executable ]
    deps += [ host_tool ]

    args = [ rebase_path(host_executable, root_build_dir) ] + invoker.args
  }
}
