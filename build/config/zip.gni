# Create a zip archive of the inputs
#
# variables:
#   inputs(required):
#     List of input files relative to the current directory.
#   output(required):
#     File name to write
#   base dir(required):
template("zip") {
  action(target_name) {
    script = "//build/android/gn/zip.py"
    depfile = "$target_gen_dir/$target_name.d"
    inputs = invoker.inputs

    outputs = [
      depfile,
      invoker.output,
    ]

    assert(defined(invoker.inputs))
    rebased_inputs = rebase_path(invoker.inputs, root_build_dir)

    assert(defined(invoker.output))
    rebased_output = rebase_path(invoker.output, root_build_dir)

    args = [
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--inputs=$rebased_inputs",
      "--output=$rebased_output",
    ]
    if (defined(invoker.base_dir)) {
      args += [
        "--base-dir",
        rebase_path(invoker.base_dir, root_build_dir),
      ]
    }
    forward_variables_from(invoker,
                           [
                             "test_only",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])
  }
}

template("zipzip") {
  action(target_name) {
    script = "//build/android/gn/zipzip.py"
    depfile = "$target_gen_dir/$target_name.d"

    inputs = []
    if (defined(invoker.input_zips)) {
      inputs += invoker.input_zips
    }

    outputs = [
      depfile,
      invoker.output,
    ]

    _base_dir = "$target_gen_dir/$target_name/zipzip"
    rebased_output = rebase_path(invoker.output, root_build_dir)

    args = [
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--output",
      rebased_output,
      "--base-dir",
      rebase_path(_base_dir, root_build_dir),
    ]
    if (defined(invoker.input_zips)) {
      rebased_input_zips = rebase_path(invoker.input_zips, root_build_dir)
      args += [ "--inputs=$rebased_input_zips" ]
    }
    if (defined(invoker.args)) {
      args += invoker.args
    }

    forward_variables_from(invoker,
                           [
                             "test_only",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])
  }
}
